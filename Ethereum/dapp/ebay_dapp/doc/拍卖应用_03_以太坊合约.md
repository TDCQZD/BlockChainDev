# 拍卖应用_03_以太坊合约

## 构建Truffle项目

首先要做的构建 truffle 项目。按照下面的指示创建 truffle 项目：
```
$ mkdir ebay_dapp
$ cd ebay_dapp
$ truffle unbox webpack
$ rm contracts/ConvertLib.sol contracts/MetaCoin.sol
```

**在合约中实现的几个用户场景如下：**

1. 用户应该能够将产品添加到区块链。我们首先会定义想要一个在链上存储的所有产品细节的结构。

2. 实现将产品加入区块链的函数。

3. 实现出价（bid）的功能，任何都可以对商品提交一个不公开的竞价。

4. 实现证明出价金额的公开函数（reveal function）

## 产品(存储产品和元数据的数据结构)

在 contracts 目录下创建一个新的文件 EcommerceStore.sol，将代码内容添加到里面。
`EcommerceStore.sol`
```
pragma solidity ^0.4.13;

contract EcommerceStore {
 enum ProductStatus { Open, Sold, Unsold }
 enum ProductCondition { New, Used }

 uint public productIndex;
 mapping (address => mapping(uint => Product)) stores;
 mapping (uint => address) productIdInStore;

 struct Product {
  uint id;
  string name;
  string category;
  string imageLink;
  string descLink;

  uint auctionStartTime;
  uint auctionEndTime;
  uint startPrice;
  address highestBidder;
  uint highestBid;
  uint secondHighestBid;
  uint totalBids;

  ProductStatus status;
  ProductCondition condition;
 }

 constructor() public {
  productIndex = 0;
 }
}
```
编写本部分代码后，用`truffle compile ` 编译合约，确保本部分代码没有任何问题。
> debug 合约里的问题可能非常麻烦。推荐每一部分代码完成后都编译一下,随时解决掉Bug

### 代码分析：
1. struct Product: 当用户想要在商店列出一个商品时，他们必须要输入关于产品的所有细节(编号(id)、名称(name)和分类(category))。我们将所有的产品信息存储在一个 struct 里面。
    - struct 里面的大部分元素都应当清晰明了。你应该能够注意到有两个元素 imageLink 和 descLink。除了在链上存储产品图片和大的描述文本，我们还会存储 IPFS 链接（下面会详细介绍），当我们要渲染网页时，会从这些链接中获取这些细节。
    - 同时还在struct 里面保存了与拍卖相关的细节，比如：起拍价格(startPrice)、拍卖开始时间(auctionStartTime)、拍卖结束(auctionEndTime)、最高竞价(highestBid)、第二高竞价(secondHighestBid)、参与竞拍的竞拍者人数(totalBids)、最高竞价的地址(highestBidder)等。
  

2. enum: 很多编程语言都有枚举（enum）的概念。它是一个用户定义类型，可以被转换为整型。在我们的代码中，我们会将产品的条件和状态存储为枚举类型。
    - ProductStatus.Open 在链上存储为 0，表示该产品的初始状态;ProductStatus.Sold 在链上存储为 1，表示该产品的已售出;ProductStatus.Unsold  在链上存储为 2，表示该产品未售出。
    - ProductCondition.New 在链上存储为 0，表示该产品是新品;ProductCondition.Used在链上存储为 1，表示该产品是二手产品.


3. productIndex: 表示产品ID,我们会给加入到商店的每个商品赋予一个 id。该字段相当于一个计数器，每当一个商品加入到商店则加 1 。

4. productIdInStore: 这是一个 mapping，用于跟踪哪些商品在哪个商店。

5. stores mapping: 任何人都可以查看商店里的产品。我们通过 mapping 跟踪谁插入了商品。键位商家的账户地址，值为 productIndex 到 Product 结果的 mapping。比如，现在商店还没有任何商品。账户地址为(0x64fcba11d3dce1e3f781e22ec2b61001d2c652e5) 的用户向店里添加了一个 iPhone，他想要卖掉这个 iPhone。我们的 stores mapping 现在就会是：  `0x64fcba11d3dce1e3f781e22ec2b61001d2c652e5 => {1 => "struct with iphone details"}`


### 向商店添加商品
接下来我们实现如何将产品添加到区块链并进行检索。

```
function addProductToStore(string _name, string _category, string _imageLink, string _descLink, uint _auctionStartTime,
  uint _auctionEndTime, uint _startPrice, uint _productCondition) public {
  require (_auctionStartTime < _auctionEndTime);
  productIndex += 1;
  Product memory product = Product(productIndex, _name, _category, _imageLink, _descLink, _auctionStartTime, _auctionEndTime,
                   _startPrice, 0, 0, 0, 0, ProductStatus.Open, ProductCondition(_productCondition));
  stores[msg.sender][productIndex] = product;
  productIdInStore[productIndex] = msg.sender;
}

function getProduct(uint _productId) view public returns (uint, string, string, string, string, uint, uint, uint, ProductStatus, ProductCondition) {
  Product memory product = stores[productIdInStore[_productId]][_productId];
  return (product.id, product.name, product.category, product.imageLink, product.descLink, product.auctionStartTime,
      product.auctionEndTime, product.startPrice, product.status, product.condition);
}
```
### 代码分析

1. 新建一个叫做 addProductToStore 的函数，参数为构建 product 结构的所需内容（除了出价相关的变量）。

2. 与上一节讨论的 productIndex 类似，计数加 1。

3. 使用 require 来验证 auctionStartTime 小于 auctionEndTime。

4. 初始化 Product 结构，并用传入函数的参数进行填充。

5. 将初始化后的结构存储在 stores mapping。

6. 同时在 productionIdInStore mapping 中记录是谁添加了商品。

7. 创建一个叫做 getProduct 的函数，它将 productId 作为一个参数，在 stores mapping 中查询商品，返回商品细节，

> 每一轮拍卖我们会以秒存储开始和结束时间。

> startPrice 存储的单位为 wei。

注意：我们在两个函数中都用了一个叫做 `memory` 的关键字来存储商品。之所以用这个关键字，是为了告诉 EVM 这个对象仅作为临时变量。一旦函数执行完毕，该变量就会从内存中清除。

### 控制台交互
现在，我们来测试实现的代码。本教程使用ganache模拟区块链，你也可以使用Geth开启私链进行测试。
如果你还没有启动 ganache，启动 ganache 并部署合约，看一下你是否能与合约交互。

1. 打开 terminal 启动 ganache。

2. 编辑 migration 文件，保存并将合约部署到区块链。

3. 启动 truffle 控制台并向区块链添加一个商品。你可以给图片和描述链接随机输入一些内容（在实现 IPFS 的相关功能呢后，我们会来改进这一点）。

4. 通过 product id 检索你插入的商品（由于这是你添加的第一个商品，所以 product id 将会是 1）。

**示例流程如下：**

- terminal 1 安装并启动ganache-cli
```
$ npm install -g ganache-cli
$ ganache-cli
terminal 2
```
- 将migrations/2_deploy_contracts.js修改为：
```
var EcommerceStore = artifacts.require("./EcommerceStore.sol");

module.exports = function(deployer) {
 deployer.deploy(EcommerceStore);
};
```
- 然后编译合约并部署到区块链上
    ```
    $ truffle compile
    $ truffle migrate
    ```
    如果运行truffle migrate报错，那么需要修改truffle.js中的地址为本地跑起来ganache-cli的地址。

    ```
    module.exports = {
    networks: {
        development: {
        host: "127.0.0.1", // IP
        port: 8545, // 注意端口号
        network_id: "*" // Match any network id
        }
    }
    };
    ```
- 运行`truffle console`，进入truffle控制台，运行以下命令。
    ```
    truffle(development)>  amt_1 = web3.toWei(1, 'ether');
    '1000000000000000000'
    truffle(development)>  current_time = Math.round(new Date() / 1000);
    truffle(development)>  EcommerceStore.deployed().then(function(i) {i.addProductToStore('iphone 6', 'Cell Phones & Accessories', 'imagelink', 'desclink', current_time, current_time + 200, amt_1, 0).then(function(f) {console.log(f)})});
    truffle(development)>  EcommerceStore.deployed().then(function(i) {i.getProduct.call(1).then(function(f) {console.log(f)})})
    ```
    
## eBay拍卖
### 拍卖是如何工作的
我们成功地向区块链添加了一个产品。现在，用户应该能够像在 eBay 上一样对你的商品进行出价。eBay 有几种不同类型的拍卖，比如增量竞价（incremental bidding），自动竞价（automatic bidding）等等。下面是 eBay 自动竞价的一个工作案例：

比如说一个商品标价为 10 美元。只要高于 10 美元，你可以任意出价：

1. Alice 出价 10.50 美元。那么，她就是拍卖的一个有力竞争者。

2. Mary 现在看到的出价是 10.50 美元，但是她出价 15 美元。尽管她出价 15 美元，但是 eBay 显示的最高价格为 10.75 美元。虽然 Alice 输掉了出价，但是她可以再次竞价。

3. John 现在看到的出价是 10.75 美元，然后他出价 12 美元，但是 eBay 会代表 Mary 出价（因为 Mary 已经出价 15 美元），并将价格提升到 12.25 美元（比 John 的出价高了 0.25 美元）。所以，John 输掉了竞价。

4. 现在 Alice 打算出价 17 美元。因为 Alice 的出价高于 Mary，eBay 将出价调整到 15.25 美元（比 Mary 的出价高 0.25 美元）。Mary 输掉了竞价（如果她想的话可以再次出一个更高的价）

5. 没有人再出价了，所以 Alice 赢得了拍卖。即使 Alice 出价 17 美元，但是她只需要支付 15.25 美元。
### 暗标拍卖
我们刚刚学的拍卖类型叫做 Vickery Auction。

任何人都可以监控交易来观察是否有其他人为一个商品出价。回到上一节的例子，当 Mary 出价 15 美元时，除了 Mary 和 eBay 没有人知道她到底出价多少。但是在以太坊上，每个人都会知道她出价多少，这实际上就会变成一个公开拍卖。为了解决这个问题，我们会使用一个稍微不同于 Vickery auction 的形式，出价者提交一个经过加密的竞价，这样就不会有人知道出价到底是多少。在拍卖结束的时候，所有参与竞价的人都出示自己的出价，大家就可以看到每个人都出价多少。合约会验证密封出价与公开出价是否一致，然后决定最终的赢家。赢家支付金额为第二高的出价。这与 ENS 的拍卖非常类似。

另一个非常不一样的地方是，在 eBay 中，当你出价时，你提交的是赢得竞价的金额，但是实际支付金额并不是所提交的出价。我们的情况则不同，当用户出价的时候必须同时发送 ETH。在以太坊中，所谓出价者就是账户地址而已，如果你通过支付来出价，就无法保证最高出价者会实际赢得拍卖。当竞价结束，所有输掉竞价的人将会收回各自出价的 ETH。

还是上一节的例子，让我们来看一下它在区块链上到底是如何工作的。

> Alice 为商品出价 10.50 美元

在我们的系统中，如果 Alice 想要出价 10.50 美元，她将会对出价进行隐藏 `sha3($10.50, "secretstring")`，产生一个哈希 比如说 `3fc3ac1afb93b6c29dc1a7d03cbff392ab89638475ed0fe7a3923facfe1dab67`（我们会在下一节学习哈希的有关细节）。然后她将这个字符串发送出去，同时发送价值 15 美元的 ETH。看到这笔交易的任何人就都知道了她发送了 15 美元，但是没有人知道她只出价 10.50 美元。

> Mary 现在看到出价是 10.50 美元，但是她送出了 15 美元。

在这种情况下，Mary 并不知道 Alice 出价 10.50 美元。她知道 Alice 送出了 15 美元。如果 Mary 想要出价 15 美元，她可以对 `sha3($15, "marysecretstring")` 进行哈希，并发送哈希后的字符串，同时还有 15 美元或者更多的 ETH。

类似地，每个人都可以对想要购买的商品进行出价。

注意，用户可能会发送一个小于实际出价的数额来迷惑其他人。比如：Alice 出价 `sha3($30, "secretstring")`，但是实际只给合约转了 20 美元。在这种情况下，当她揭示出价时，合约会将这些钱归还回去，因为这是一个无效的出价。

> John 现在看到目前的出价是 10.75 美元，而他出价 12 美元

在这种情况下，John 仅出价 12 美元，因为这就是它愿意支付的金额。

> 现在 Alice 决定出价 17 美元

尽管 Alice 已经出价了一次，她仍然可以再次出价。失败的任何报价，Alice 都会取回自己的钱。
### 揭示报价
一旦拍卖结束，所有的出价者都必须揭示各自报价。为了揭示报价，出价者必须向合约发送他们出价的金额和 secret string（他们用 secret string 对出价进行了哈希）。合约通过将报价金额与 secret string 进行组合构造出哈希后的报价，然后与出价者之前所发送的哈希后的字符串进行匹配。如果匹配成功，则出价有效。否则出价无效，合约会返还相应的资金。

还是回到之前的例子，来看一下揭示报价是如何工作的。

**Alice 揭示报价**

Alice 通过向合约发送 10.50 美元和 “secretstring” 来揭示她的报价。合约使用同一个算法来生成哈希。在这个例子中，所生成的哈希会与 Alice 所发送的3fc3ac1afb93b6c29dc1a7d03cbff392ab89638475ed0fe7a3923facfe1dab67 一样。由于这是一个有效出价并且 Alice 发送了 15 美元，合约会记录它为有效出价，并将 15 - 10.5 = 4.5 美元返还给 Alice。

**Mary 揭示报价**

类似地，Mary 也要揭示她的出价。因为她出价 15 美元，她就是最高的出价者。合约会替换掉 Alice，Mary 成为最高的出价者，Alice 成为第二高的出价者。因为 Alice 没有赢得竞价，所以她会拿回自己所有的钱。

**John 揭示报价**

John 仅出价 12 美元。当揭示报价时，因为 John 输掉了竞价所以他会立刻收到返还的资金。

在本例中， Mary 赢得竞价，并支付 12 美元（第二高的报价）。
### 拍卖的合约代码
现在我们已经知道出价和揭示出价是如何工作了。下面让我们来实现这些功能。

我们需要有一种途径来存储用户的出价。让我们来创建一个 struct 保存出价信息。注意 struct 里面的 value 字段是出价者实际发送 ETH 的数量，而不是当前实际出价的数量。当前出价的数量被加密了。只有发送的数量是已知的，它会被用于填充 value 字段。

为了方便地查询用户给哪个商品出价，出价多少。让我们给 product struct 加入一个` mapping mapping (address => mapping (bytes32 => Bid)) bids;`。键为出价者的地址，值为哈希后的出价字符串到 `bid struct` 的` mapping`。
`Bid Struct`
```
struct Bid {
  address bidder;
  uint productId;
  uint value;
  bool revealed;
}
```
`Product Struct`
```
struct Product {
  ....
  ....
  mapping (address => mapping (bytes32 => Bid)) bids;
}
```

**出价**

bid 函数有两个参数，product id 和加密后的 bid 字符串。bid 函数本身非常直观。我们从 stores mapping 检索产品，构建 bid struct 并把它加入到 mapping（我们上面刚刚初始化了）。我们有一些验证（require 语句）也很直观。你会注意到代码里的关键词 now。它仅仅是当前块的时间戳，也就是，当 bid 函数被调用时，表明一笔交易被创建。这笔交易被矿工打包到块里。每个块都有一个对应的时间戳（用来告诉你这个块被挖出来的时间）。now 就等同于那个时间戳。

在上两节，我们谈到了对 bid 进行哈希。sha3 是一个密码学上的哈希函数，对于任何长度的任意字符串，它都可以生成一个固定长度的唯一字符串。所生成的字符串对于给定的任意字符串都是独一无二的，也就是说，没有两个任意字符串能够通过 sha3 哈希算法生成一样固定长度的哈希。

让我们来看一下为什么这对我们的场景十分有用（生成密封的出价）。为了生成一个密封出价，我们使用了 `ethereumjs-util library's sha3 function`(你也可以使用Web3中sha3)。如果 Alice 想要生成一个出价，她只需要调用 sha3 函数，传入她打算出价的数量和 secret。
```
sha3("10.5" + "secretstring").toString('hex') => c2f8990ee5acd17d421d22647f20834cc37e20d0ef11087e85774bccaf782737
```
这是传入 bid() 函数的 bytes32 字符串。任何看到该字符串的人都不知道 Alice 的出价是 10.5。

`Bid function`
```
function bid(uint _productId, bytes32 _bid) payable public returns (bool) {
  Product storage product = stores[productIdInStore[_productId]][_productId];
  require (now >= product.auctionStartTime);
  require (now <= product.auctionEndTime);
  require (msg.value > product.startPrice);
  require (product.bids[msg.sender][_bid].bidder == 0);
  product.bids[msg.sender][_bid] = Bid(msg.sender, _productId, msg.value, false);
  product.totalBids += 1;
  return true;
}
```
**揭示出价**

`Reveal function`
```
function revealBid(uint _productId, string _amount, string _secret) public {
 Product storage product = stores[productIdInStore[_productId]][_productId];
 require (now > product.auctionEndTime);
 bytes32 sealedBid = sha3(_amount, _secret);

 Bid memory bidInfo = product.bids[msg.sender][sealedBid];
 require (bidInfo.bidder > 0);
 require (bidInfo.revealed == false);

 uint refund;

 uint amount = stringToUint(_amount);

 if(bidInfo.value < amount) {
  // They didn't send enough amount, they lost
  refund = bidInfo.value;
 } else {
  // If first to reveal set as highest bidder
  if (address(product.highestBidder) == 0) {
   product.highestBidder = msg.sender;
   product.highestBid = amount;
   product.secondHighestBid = product.startPrice;
   refund = bidInfo.value - amount;
  } else {
   if (amount > product.highestBid) {
    product.secondHighestBid = product.highestBid;
    product.highestBidder.transfer(product.highestBid);
    product.highestBidder = msg.sender;
    product.highestBid = amount;
    refund = bidInfo.value - amount;
   } else if (amount > product.secondHighestBid) {
    product.secondHighestBid = amount;
    refund = bidInfo.value;
   } else {
    refund = bidInfo.value;
   }
  }
 }
 product.bids[msg.sender][sealedBid].revealed = true;

 if (refund > 0) {
  msg.sender.transfer(refund);
 }
}
```
revealing 函数稍显复杂。为了更好地理解代码，让我们首先来捋一下逻辑。揭示出价就是要告诉合约你出价了多少。方式就是将的 secret string 和你打算出价的数量发送给合约。合约将同样的 sha3 算法应用于出价数量和 secret，并检查所生成的哈希是否在 bids mapping 里面。当执行检查的时候，可能会出现以下场景：

1. 没有找到相关的哈希。这意味着用户尝试揭示不曾出价过的数量。在这种情况下，仅抛出一个异常（revealBid 函数的第 6 行）

2. 出价数量小于发送数量：比如用户出价 10 美元，但是只发送了 5 美元。因为这是无效的，所以我们只需要将这 5 美元返回给用户即可。

3. 出价数量大于等于发送数量：这是一个有效出价。现在我们会检查是否应该记录此次出价。

4. 首次揭示：如果这是第一个有效的出价揭示，我们会把它记录为最高出价，同时记录是谁出的价。我们也会将第二高的出价设置为商品的起始价格（如果没有其他揭示报价，就由这个用户支付起始价格。还记得赢家总是支付第二高的价格吗？）。

5. 更高的出价：如果用户揭示了出价，并且他们的出价高于现有所揭示的最高出价，我们将会记录该出价者，将其出价记录为最高出价，并设置第二高的出价为旧的出价数量。

6. 更低的出价：如果出价比最高出价要低，这是一个会失败的出价。但是我们也会检查它是否低于第二高的出价。如果是的话，只需要返还资金即可，因为他们已经输掉了竞价，否则将该出价设置为第二高的出价。

在所有情况中，我们会返还发送数量与实际出价的差额，也就是，如果 Alice 出价 10 美元，但是发送了 15 美元，在揭示出价以后，将会返回给 Alice 5 美元。

**getter 函数**
让我们也实现两个简单的 getter 函数，分别返回最高出价者信息和一个商品的总出价。这些信息将会用于在网页显示出价信息，同时为了有助于在 truffle 控制台进行测试。

`Getter & Helper functions`
```
function highestBidderInfo(uint _productId) view public returns (address, uint, uint) {
  Product memory product = stores[productIdInStore[_productId]][_productId];
  return (product.highestBidder, product.highestBid, product.secondHighestBid);
}

function totalBids(uint _productId) view public returns (uint) {
  Product memory product = stores[productIdInStore[_productId]][_productId];
  return product.totalBids;
}

function stringToUint(string s) pure private returns (uint) {
  bytes memory b = bytes(s);
  uint result = 0;
  for (uint i = 0; i < b.length; i++) {
    if (b[i] >= 48 && b[i] <= 57) {
      result = result * 10 + (uint(b[i]) - 48);
    }
  }
  return result;
}
```
### 控制台交互
我们会用 ethereumjs-util 库来生成出价的哈希。首先来安装这个库。将库添加到 package.json 并安装。
```
"devDependencies": {
  ....
  ....
  "ethereumjs-util": "5.1.2"
}
```
```
$ npm install
$ truffle migrate --reset
$ truffle console
```
你已经在 ganache 部署了合约的第一个版本。你可以重启 ganache 并运行 truffle migrate，或者传入 --reset 选项来重新部署合约。

1. 让我们首先向区块链插入一个商品（拍卖结束时间从现在算起 200 秒）

2. 我们已经有了 10 个测试账户，所以让我们从不同账户出价几次。

当你对商品出价以后，使用 web3.eth.getBalance 检查 accounts[0] 和 accounts[1] 的余额。你会注意到它们的余额大概是 97 ETH 和 96 ETH。他们打算出价 2 ETH 和 3 ETH，但是分别发送了 3 ETH 和 4 ETH。如果代码如期工作，当我们揭示出价时，差价应该返还给这些账户。

1. 我们会等待直到拍卖结束（在本例中我们已经设置结束时间为现在起 200 秒），然后揭示所有出价。

2. 我们将会使用 getter 方法来查看是谁赢得了拍卖。

当执行 highestBidderInfo 函数，你应该看到 accounts[2] 为最高出价 3 ETH 的出价者（赢家），第二高的出价是 2 ETH。

此时，所有失败的出价者将会收到返回的出价资金。赢家的出价数量仍然在合约里。在托管服务一节，我们将会添加将 ETH 从合约转移到单独的托管合约的功能。

```
truffle(development)>  amt_1 = web3.toWei(1, 'ether');
'1000000000000000000'
truffle(development)>  current_time = Math.round(new Date() / 1000);
truffle(development)>  EcommerceStore.deployed().then(function(i) {i.addProductToStore('iphone 6', 'Cell Phones & Accessories', 'imagelink', 'desclink', current_time, current_time + 200, amt_1, 0).then(function(f) {console.log(f)})});
truffle(development)>  EcommerceStore.deployed().then(function(i) {i.getProduct.call(1).then(function(f) {console.log(f)})})
truffle(development)>  Eutil = require('ethereumjs-util');
truffle(development)>  sealedBid = '0x' + Eutil.sha3((2 * amt_1) + 'mysecretacc1').toString('hex');
truffle(development)>  EcommerceStore.deployed().then(function(i) {i.bid(1, sealedBid, {value: 3*amt_1, from: web3.eth.accounts[1]}).then(function(f) {console.log(f)})});
truffle(development)>  sealedBid = '0x' + Eutil.sha3((3 * amt_1) + 'mysecretacc2').toString('hex');
truffle(development)>  EcommerceStore.deployed().then(function(i) {i.bid(1, sealedBid, {value: 4*amt_1, from: web3.eth.accounts[2]}).then(function(f) {console.log(f)})});

truffle(development)>  web3.eth.getBalance(web3.eth.accounts[1])
truffle(development)>  web3.eth.getBalance(web3.eth.accounts[2])

truffle(development)>  EcommerceStore.deployed().then(function(i) {i.revealBid(1, (2*amt_1).toString(), 'mysecretacc1', {from: web3.eth.accounts[1]}).then(function(f) {console.log(f)})})
truffle(development)>  EcommerceStore.deployed().then(function(i) {i.revealBid(1, (3*amt_1).toString(), 'mysecretacc2', {from: web3.eth.accounts[2]}).then(function(f) {console.log(f)})})

truffle(development)>  EcommerceStore.deployed().then(function(i) {i.highestBidderInfo.call(1).then(function(f) {console.log(f)})})
```
## 合约完整代码如下：
```
pragma solidity ^0.4.23;

contract EcommerceStore {

    // 产品状态存储，在链上存储为 0，已售出则为 1
    enum ProductStatus { Open, Sold, Unsold}
    // 产品的条件 new old
    enum ProductCondition {New, Used}
    //  商店的商品id，每当一个商品加入到商店则加 1，相当于计数器
    uint public productIndex;
    // 跟踪商户哪些商品在哪个商店
    mapping (uint => address) productIdStore;
    // 列出商店里的产品
    mapping (address => mapping(uint => Product)) stores;


    // 商品详情
    struct Product{
        uint id;
        string name;
        string category;
        string imageLink; // 产品图片连接
        string descLink; // 大的描述文本连接
        // 拍卖相关
        uint auctionStartTime;
        uint auctionEndTime;
        uint startPrice;

        address highestBidder; //竞拍出最高价的地址
        uint highestBid;// 竞拍最高价
        uint secondHighestBid;// 竞拍第二高价
        uint totalBids; //参与竞价人数
        ProductStatus status;  //产品竞拍状态
        ProductCondition condition;  // 竞拍产品条件
        mapping (address => mapping(bytes32 => Bid)) bids; // 查询用户竞拍过的产品，出价多少
    }
    // 竞价信息 
    struct Bid{
        address bidder;
        uint productId;
        uint value; // 出价者实际发送 ETH 的数量，而不是当前实际出价的数量
        bool revealed; // 揭示报价
    }
    constructor() public{
        productIndex = 0;
    }
    // 向商店添加商品
    function addProductToStore(string _name, string _category, string _imageLink, string _descLink, uint _auctionStartTime,uint _auctionEndTime, uint _startPrice, uint _productCondition) public {
        require(_auctionStartTime < _auctionEndTime,"AuctionEndTime should be late than AuctionStartTime");
        productIndex +=1;
        Product memory product = Product(productIndex,_name,_category,_imageLink,_descLink,_auctionStartTime,_auctionEndTime,_startPrice,0,0,0,0,ProductStatus.Open,ProductCondition(_productCondition));
        // 商店商户信息
        stores[msg.sender][productIndex] = product;
        // 商店商品信息
        productIdStore[productIndex] = msg.sender;
    }

    // 在 stores mapping 中查询商品
    function getProduct(uint _productId) view public returns (uint, string, string, string, string, uint, uint, uint, ProductStatus, ProductCondition) {
         Product memory product = stores[productIdStore[_productId]][_productId];
         return (product.id, product.name, product.category, product.imageLink, product.descLink, product.auctionStartTime, product.auctionEndTime, product.startPrice, product.status, product.condition);
    }
    // 出价
    function bid(uint _productId, bytes32 _bid) payable public returns (bool) {
        Product storage product = stores[productIdStore[_productId]][_productId];
        require(now >= product.auctionStartTime,"Current time should be late than auctionStartTime");
        require(now <= product.auctionEndTime,"Current time should be earlier than auctionEndTime");
        
        require(msg.value > product.startPrice);
        require(product.bids[msg.sender][_bid].bidder == 0);
        
        product.bids[msg.sender][_bid] = Bid(msg.sender, _productId, msg.value, false);
        product.totalBids += 1;
        return true;
    }
    
    // 揭示出价
    function revealBid(uint _productId, string _amount, string _secret) public {
        Product storage product = stores[productIdStore[_productId]][_productId];
        require(now >= product.auctionEndTime);
        bytes32 sealedBid = sha3(_amount,_secret);
        Bid memory bidInfo = product.bids[msg.sender][sealedBid];
        require(bidInfo.bidder > 0,"bidder should exist");
        require(bidInfo.revealed == false,"Bid should not be revealed");
        
        uint refund;
        uint amount = stringToUint(_amount);
        if (bidInfo.value < amount){
            refund = bidInfo.value;
        }else{
            if (address(product.highestBidder) == 0) {// 首次揭示
                product.highestBidder = msg.sender;
                product.highestBid = amount;
                product.secondHighestBid = product.startPrice;
                
                refund = bidInfo.value - amount;
            } else {
                if (amount > product.highestBid){
                    product.secondHighestBid = product.highestBid;
                    product.highestBidder.transfer(product.highestBid);
                    product.highestBidder = msg.sender;
                    product.highestBid = amount;
                    refund = bidInfo.value - amount;
                }else if (amount >product.secondHighestBid) {
                    product.secondHighestBid = amount;
                    refund = bidInfo.value;
                }else{
                    refund = bidInfo.value;  
                }
            }
        }
        product.bids[msg.sender][sealedBid].revealed = true;
        
        if (refund > 0) {
            msg.sender.transfer(refund);
        }
    }
    //
    function highestBidderInfo(uint _productId) view public returns (address, uint, uint) {
        Product memory product = stores[productIdStore[_productId]][_productId];
        return (product.highestBidder, product.highestBid, product.secondHighestBid);
    }

    function totalBids(uint _productId) view public returns (uint) {
        Product memory product = stores[productIdStore[_productId]][_productId];
        return product.totalBids;
    }
    // 字符串装数字的工具方法
    function stringToUint(string s) pure private returns (uint) {
        bytes memory b = bytes(s); //把字符串转换为字节数组
        uint result = 0;
        for (uint i = 0; i < b.length; i++) {

            if (b[i] >= 48 && b[i] <= 57) {// 
                result = result * 10 + (uint(b[i]) - 48);
            }
        }
        return result;
    }
}
```